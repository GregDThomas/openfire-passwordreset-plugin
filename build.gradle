import com.github.spotbugs.snom.Confidence
import com.github.spotbugs.snom.Effort
import com.github.spotbugs.snom.SpotBugsReport
import com.github.spotbugs.snom.SpotBugsTask

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'checkstyle'
    id 'com.github.spotbugs' version '4.8.0'
}
apply from: 'build.openfire-plugin.gradle'

ext.lombokVersion = '1.18.36'
ext.junitVersion = '5.3.1'

group = 'org.igniterealtime.openfire.plugins'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
}

checkstyle {
    toolVersion '8.31'
    maxWarnings 0
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
    exclude '**/org/jivesoftware/openfire/plugin/passwordreset/jsp/**'
}

spotbugs {
    effort = Effort.MAX
    reportLevel = Confidence.LOW
    ignoreFailures.set false
    extraArgs.add "-longBugCodes"
}

tasks.withType(SpotBugsTask) {
    enabled false // For now
    reports(({
        text.enabled = true
    } as Closure<NamedDomainObjectContainer<? extends SpotBugsReport>>))

    //noinspection GroovyAssignabilityCheck
    task "${it.name}Report" {
        def input = file(reports.getByName("TEXT").destination)
        inputs.file input
        doLast {
            input.readLines().forEach {
                println(it)
            }
        }
    }
 //   it.finalizedBy "${it.name}Report"
}

spotbugsMain {
    classes = classes.filter {
        !it.path.contains(new File('/org/jivesoftware/openfire/plugin/passwordreset/jsp/').path)
    }
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation 'com.github.bbottema:emailaddress-rfc2822:2.1.4'

    testImplementation 'com.github.spotbugs:spotbugs-annotations:4.0.1'
    testImplementation platform('org.junit:junit-bom:5.6.2')
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.3.3'
    testImplementation 'org.assertj:assertj-core:3.11.1'
}

// See https://github.com/jeremylong/DependencyCheck/issues/2764#issuecomment-680680558 flr an expla nation
dependencies {
    components {
        withModule('org.dom4j:dom4j', ClearDependencies)
    }
}

class ClearDependencies implements ComponentMetadataRule {
    void execute(ComponentMetadataContext context) {
        context.details.allVariants { withDependencies { clear() } }
    }
}